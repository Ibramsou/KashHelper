plugins {
    id 'java'
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.36.0"
    id 'signing'
    id("com.gradleup.shadow") version "9.3.1" apply false
}

group = 'fr.ibrakash.helper'
version = '1.0.2'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT"
    compileOnly 'org.spongepowered:configurate-core:4.2.0'
    compileOnly 'org.spongepowered:configurate-yaml:4.2.0'
    compileOnly 'org.spongepowered:configurate-hocon:4.2.0'
    compileOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.20.0'
}

mavenPublishing {
    publishToMavenCentral(true)

    coordinates("xyz.imrine", "Kash-Helper", version.toString())

    pom {
        name.set("KashHelper")
        description.set("Kash Helper is an helper library")
        url.set("https://github.com/Ibramsou/KashHelper")

        licenses {
            license {
                name.set("The Apache License, Version 2.0")
                url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
            }
        }

        developers {
            developer {
                id.set("Bramsou")
                name.set("Ibrahim")
                email.set("braimsou@gmail.com")
            }
        }

        scm {
            url.set("https://github.com/Ibramsou/KashHelper")
            connection.set("scm:git:git://github.com/Ibramsou/KashHelper.git")
            developerConnection.set("scm:git:ssh://github.com/Ibramsou/KashHelper.git")
        }
    }
}

def signingKeyFile = findProperty("signingInMemoryKeyFile")?.toString()
def signingPassword = findProperty("signingInMemoryKeyPassword")?.toString()

signing {
    def keyText = file(signingKeyFile).text
    useInMemoryPgpKeys(keyText, signingPassword)
    sign(publishing.publications)
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}