plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.vanniktech.maven.publish.base" version "0.36.0"
    id 'signing'
    id("com.gradleup.shadow") version "9.3.1"
}

group = 'fr.ibrakash.helper'
version = '1.6.0'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url 'https://repo.xenondevs.xyz/releases' }
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // Paper API
    compileOnly "io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT"

    // Configuration Libraries
    compileOnly 'org.spongepowered:configurate-core:4.2.0'
    compileOnly 'org.spongepowered:configurate-yaml:4.2.0'
    compileOnly 'org.spongepowered:configurate-hocon:4.2.0'
    compileOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.20.0'

    // Inventory Library
    implementation "xyz.xenondevs.invui:invui:1.49"

    // SQL Libraries & drivers
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:3.3.3'
    compileOnly 'mysql:mysql-connector-java:8.0.29'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
}

tasks.jar {
    archiveClassifier.set("plain")
}

tasks.shadowJar {
    archiveClassifier.set("")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "xyz.imrine"
            artifactId = "Kash-Helper"
            version = project.version.toString()

            from components.shadow

            artifact(tasks.named("sourcesJar"))
            artifact(tasks.named("javadocJar"))
        }
    }
}

mavenPublishing {
    publishToMavenCentral(true)

    pom {
        name.set("KashHelper")
        description.set("Kash Helper is an helper library")
        url.set("https://github.com/Ibramsou/KashHelper")

        licenses {
            license {
                name.set("The Apache License, Version 2.0")
                url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
            }
        }

        developers {
            developer {
                id.set("Bramsou")
                name.set("Ibrahim")
                email.set("braimsou@gmail.com")
            }
        }

        scm {
            url.set("https://github.com/Ibramsou/KashHelper")
            connection.set("scm:git:git://github.com/Ibramsou/KashHelper.git")
            developerConnection.set("scm:git:ssh://github.com/Ibramsou/KashHelper.git")
        }
    }
}

def signingKeyFile = findProperty("signingInMemoryKeyFile")?.toString()
def signingPassword = findProperty("signingInMemoryKeyPassword")?.toString()

signing {
    if (signingKeyFile != null && file(signingKeyFile).exists()) {
        def keyText = file(signingKeyFile).text
        useInMemoryPgpKeys(keyText, signingPassword)
        sign(publishing.publications)
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}